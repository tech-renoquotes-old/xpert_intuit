{{ 
    //
    // Name:        menu_principal
    //
    // Description: Permet de publier le menu principal
    //  
    // Parameters:  none so far
	//				use cgidata 
	//				use lg
    //
    // Version:     1.0 2014-08-02 08:49:42 laplante@sednove.com initial version
    //

    %include "/extenso/functions/sn_ct.snc"; 
    %include "/extenso/functions/sn_pages.snc"; 

    
    use lg;             // Loop from the publish
    use cgidata;           // To get infos about the user who publish the page

	site_config = sql(single:true,"select * from sn_configs");
	menu_json = sql(single:true,wantarray:true,"select json from sn_menus where code = '?'", cgidata.menu);
	if menu_json.rows[0] eq '' then
		menu = [];
	else
		menu = unstringnify(menu_json.rows[0]);
	endif
}}
\{{
	use page;			// Should be defined with the uid of the current page
}}
<button id="menumobile" type="button" class="btn btn-default dropdown-toggle btn-block btn-lg" onClick="$('#top-navbar').slideToggle();">
	{{sn_ct(code:"MENU")}}
	<span class="caret"></span>
</button>
<div id="top-navbar" class="navbar navbar-default" role="navigation">
	<div class="container-fluid">

		<div class="navbar-collapse collapse">
			<ul class="nav navbar-nav">
			{{ 
				function display_menu(menu, lg) 
					for item in menu do 
						menu_item = sql(single:true,"select * from sn_menus_items where uid = '?'", item.id);
						if menu_item.rows.active eq "yes" then
							if menu_item.rows.type eq "page" then
								page = sql(single:true,"select * from ? where uid = '?'", menu_item.rows.table_name, menu_item.rows.uid_page);
								if page.nbrows == 1 then
								}} <li class="{{menu_item.rows.class}}"><a class="link" href="{{page.rows{"folder_" .+ lg.rows.lg};
									if page.rows{"page_" .+ lg.rows.lg} ne "index" then
										page.rows{"page_" .+ lg.rows.lg};
									endif
									}}">{{menu_item.rows{"name_" .+ lg.rows.lg};}}</a></li> {{
								endif
							elseif menu_item.rows.type eq "url" then
								}} <li class="{{menu_item.rows.class}}"><a target="{{menu_item.rows{"target"};}}" href="{{menu_item.rows{"url_" .+ lg.rows.lg};}}">{{menu_item.rows{"name_" .+ lg.rows.lg};}}</a></li> {{
							elseif menu_item.rows.type eq "group" then
							}} <li class="dropdown">
									<a href="#" class="dropdown-toggle" data-toggle="dropdown">{{menu_item.rows{"name_" .+ lg.rows.lg};}} <i class="fa fa-angle-down  hidden-xs hidden-sm"></i></a>
									<ul class="dropdown-menu" role="menu">
										{{
										if item.children != undefined then
											display_menu(item.children, lg);
										endif
										}}
									</ul>
								</li> 
							{{
							endif
						endif
					endfor 
				endf

				display_menu(menu, lg);
			}}
			</ul>
		</div><!--/.nav-collapse -->
	</div><!--/.container-fluid -->
</div><!--/.Static navbar -->