{{
    /*
        @title ajax_change_territories
        Description: Change the territories of a contractor.
        
        @author madupuis@sednove.com
        
        @version 1.0 2016-08-02

    */ 
    
    %include "/extenso/functions/sn_ct.snc";
    %include "/extenso/functions/sn_pages.snc";
    
    use page;
    use lg;
    %include "/site/package/all.sn";
}}
\{{
    // include the validator for contractor_form
    %include "/site/contractor_dashboard/include/change_territories_validator.sn";
    
    use extranet_user;
    cgidata = cgidata();

    // validate server side
    if (validator.validate(data:cgidata)) then
        
        error = 0;
        
        // update the main territory
        res = sql("UPDATE sr_contractor SET uid_territory='?' WHERE uid='?'", cgidata.main_territory, extranet_user.uid);
        error = error + res.sqlcode;
        if cgidata.territory !~:i cgidata.main_territory then
            cgidata.territory .+= "," .+ cgidata.main_territory;
        endif
        // delete all the territories of the contractor
        res =  sql("DELETE FROM sr_contractor_territory WHERE uid_contractor='?'", extranet_user.uid);
        // for each territory entered, insert territory
        for t in explode(",", cgidata.territory) do
            res = insert(
                table: "sr_contractor_territory",
                fields:{
                    "uid_territory":t,
                    "uid_contractor":extranet_user.uid
                }
            );
            error = error + res.sqlcode;
        endfor

        if error == 0 then
    
            // send email
            automail::notifyAdminContractorChangeTerritory(extranet_user.uid); 
        
            headers_out("X-success", 1);
        else
            headers_out("X-error", 1);
        endif

    endif

    
}}