{{
    
    /*
    
    	@title	sed_login
    	
    	Use to display Display login form and lost password form
    
    	
    	@author gamila@sednove.com
    	
    	@version 1.0 2016-06-02 10:00:00 laplante@sednove.com Initial Version
    	@version 1.1 2016-06-03 10:00:00 pascale@sednove.com Code review + page choice from table sed_login_page

    */

    %include "/extenso/functions/sn_ct.snc"; 

	use widget;
	use lg;
	config = config();
	
	templ_email = sql(single:true,"SELECT * FROM email WHERE code='sed_login_forget_password_email'");
	url_email = "/email/" .+ lg.rows.lg .+ "/";
	
    // if templ_email.rows.module ne "" then
    //     module_email = sql(single:true,"select * from sn_modules where module_code = '?'", templ_email.rows.module);
    //     url_email = "/module/" .+ module_email.rows.developer .+ "/" .+ module_email.rows.dir .+ url_email;
    // endif
    
	dir_email = "/" .+ config.site .+ url_email;
    file_email = templ_email.rows{"code"} .+ ".snc";
    
    page = sql(single:true,"SELECT url_interne FROM sed_login_page");  
    p = sql(single:true,"select * from pages where uid = '?'", page.rows.url_interne);
}}

\{{
    %include "/extenso/functions/sn_login.snc"; 
	cgidata = cgidata();
	use config;
	use page; // In template, this variable is defined.
    ch_password = 1 ;
	message = "";
	error = [];
}}

	<div class="col-sm-offset-3 col-sm-6 well">
	    <!--Logout-->
	   \{{	if cgidata.logout then }} 
        		<div class="alert alert-info" role="alert"> 
        		    {{ sn_ct("sed_login You have been logout",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}} 
        		</div>
		\{{    void sn_logout(user_table:"sr_contractor",login_table:"sr_login");
	        endif
	        
        //	if k is set than this is a link from an email to change password
    	if cgidata.k ne "" then
    		forget_user = decrypt(key:"aj58fla74jdna63g",value:cgidata.k);
    		time = time();
    		diff = time.secs - forget_user.secs;
    		finduser = sql(single:true,"SELECT * FROM sr_contractor WHERE uid = '?'", forget_user.uid);
    		
    		if finduser.nbrows != 1 || diff > 0 then
    			extranet_user = null;
    			error[length(error)] = dq(({{ sn_ct("sed_login this link is not valid or expired.",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
    		else
    			extranet_user = finduser.rows;
    			if cgidata.password ne "" && cgidata.password2 ne "" then // fields not empty 
    				if cgidata.password ne cgidata.password2 then
    					error[length(error)] = dq(({{ sn_ct("sed_login both password are different",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
    					change_password = null;
    				else
    					password = md5(cgidata.password .+ extranet_user.sn_salt_password);
    					change_password=update(tables:"sr_contractor",uid:extranet_user.uid,fields:{"password":password});	
    					if cgidata.after_changing eq "yes" then
    					    void update(tables:"sr_contractor",uid:extranet_user.uid,fields:{"change_password":"no"});
    					endif
    					message .+= dq(({{ sn_ct("sed_login password has been changed",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
    				endif
    			else // empty fields
    				change_password = null;
    				if cgidata.action ne "" then
    					error[length(error)] = dq(({{ sn_ct("sed_login empty fields",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
    				endif
    			endif
    		endif
    	endif

        // Force change password
        if cgidata.action eq "force_change_password" then
    	    change_password = null;
    	    time = time();
            secs = time.secs + 86400;
            forgot_user = decrypt(key:"aj58fla74jdna63g",value:cgidata.u);
            finduser = sql(single:true,"SELECT * FROM sr_contractor WHERE uid = '?'", forgot_user);
            link = { "uid" : finduser.rows.uid, "secs" : secs };
            cgidata.k = crypt(key:"aj58fla74jdna63g",value:link);
    	endif

        // Login
    	if cgidata.action eq "login" then
    		if cgidata.username eq "" then
    			error[length(error)] = dq(({{ sn_ct("sed_login username is missing",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
    		endif
    
    		if cgidata.password eq "" then
    			error[length(error)] = dq(({{ sn_ct("sed_login password is missing",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
    		endif
            
    		if length(error) == 0  then
    			extranet_user = sn_login(user_table:"sr_contractor",login_table:"sr_login",username:cgidata.username,password:cgidata.password);
    			
    			if extranet_user == null then
    				error[length(error)] = dq(({{ sn_ct("sed_login incorrect login",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
    				// sleep(3);
    			else
    			    
    			    // redirection page
                     if "{{p.nbrows}}" eq 1 then
                         file = "{{p.rows{'folder_' .+ lg.rows.lg}; p.rows{'page_' .+ lg.rows.lg}; }}.snc"; 
    				 else
    				     file =  "/extranet/{{lg.rows.lg}}/index.snc";
    				endif

         			if extranet_user.change_password ne "yes" then
        				    redirect(url:file);
        			else 
        				    u = crypt(key:"aj58fla74jdna63g",value:extranet_user.uid);
        				    redirect(url:"?action=force_change_password&u=".+u);
        			endif
    			endif
    		endif
    	endif

        // Reset password
    	if cgidata.action eq "reset_password" then
    		if cgidata.email eq "" then
    			error[length(error)] = dq(({{ sn_ct("sed_login email is missing",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
          		ch_password = null ;
    		else
    		    userEmail = esc(filter:"sqlqm,cleanhtml",cgidata.email);
    			finduser = sql("SELECT uid, contact_email, company_name, username FROM sr_contractor WHERE contact_email = '?' and active = 'yes'", userEmail);
    			if finduser.nbrows < 1 then
    				error[length(error)] = dq(({{ sn_ct("sed_login invalid email",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
    				sleep(3);
          			ch_password = null ;
    			else
    			    with_fullpage = 0;
    			    for contractor_user in finduser.rows do
        				email(from:"extenso@sednove.com",to:contractor_user.contact_email,
        					subject:dq(({{sn_ct(edit:false,"sed_login subject email reset password",edit:false,publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}})),
        					ct:"text/html",
        					file:"{{dir_email .+ file_email}}");
          			endfor
      				message .+= dq(({{ sn_ct("sed_login an email has been send to reset your password",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}));
          			ch_password = 1 ;
    			endif
    		endif
    	endif
}}

\{{    if cgidata.action eq "forget" || (cgidata.action eq "reset_password" && ch_password == null) then }}  
          \{{  // forget password form }}
          \{{ for err in error do}}
              <div class="alert-wrap">
                  <div class="alert alert-danger" role="alert">
                      <i class="fa fa-exclamation-triangle"></i> \{{ err }}
                  </div>
              </div>
          \{{ endfor }}
          
          <form role="form" method="post" action="?">
              <input type="hidden" name="action" value="reset_password">
              <h2>{{ sn_ct("sed_login Forget password",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}</h2>
              <div class="form-group">
                  <label for="inputEmail3" class="control-label">{{ sn_ct("sed_login Please enter your email",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}</label>
                  <input type="email" class="form-control" id="email" name="email">
              </div>
              
              <div class="form-group">
                	  <a class="btn btn-default pull-left" href="?">
                          <i class="fa fa-reply left"></i>{{ sn_ct("sed_login Cancel",edit:true,publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}
                      </a>  
                      <button type="submit" class="btn btn-primary pull-right">
                          <i class="fa fa-check-circle left red"></i>{{ sn_ct("sed_login Submit",edit:false,publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}
                      </button>
              </div>
              <div class="clearfix"></div>
          </form>
    \{{ else }}

      \{{ if message ne "" then }}
          <div class="alert-wrap">
            <div class="alert alert-success" role="alert">
                <i class="fa fa-check-circle left"></i> \{{ message }}
            </div>
          </div>
      \{{ endif }}

        \{{ for err in error do}}
          <div class="alert-wrap">
              <div class="alert alert-danger" role="alert">
                  <i class="fa fa-exclamation-triangle"></i> \{{ err }}
              </div>
          </div>
        \{{ endfor }}

      
      \{{ 
          //   Password reset form  
          if extranet_user != null && change_password == null then }}
            <h2>{{ sn_ct("sed_login Password reset",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}} \{{extranet_user.email}}</h2>
            <form role="form" action="?" method="post">
              <input type="hidden" name="action" value="change_password">
              <input type="hidden" name="k" value="\{{cgidata.k}}">
              
               \{{ if cgidata.action eq "force_change_password" then }}
                    <input type="hidden" name="after_changing" value="yes" />
              \{{endif}}
              
              <div class="form-group\{{ if length(error) > 0 then }} has-error\{{ endif }}">
                  <label for="password" class="control-label">{{ sn_ct("sed_login Password",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}</label>
                  <input type="password" class="form-control" id="password" name="password">
              </div>
              
              <div class="form-group\{{ if length(error) > 0 then }} has-error\{{ endif }}">
                  <label for="password2" class="control-label">{{ sn_ct("sed_login Repeat Password",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}</label>
                  <input type="password" class="form-control" id="password2" name="password2">
              </div>
              
              <div class="form-group">
                <a class="btn btn-default pull-left" href="?">
                    <i class="fa fa-reply left"></i> {{ sn_ct("sed_login Return to login",edit:false,publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}
                </a>
                <button id="btn_go" type="submit" class="btn btn-primary pull-right">
                  <i class="fa fa-check-circle left red"></i>{{ sn_ct("sed_login change password",edit:false,publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}
                </button>
              </div>
             <div class="clearfix"></div>
          </form>

      \{{ else 
          //   Connexion form
      }}
          
          <form role="form" action="?" method="post">
              <input type="hidden" name="action" value="login">
              <h2>{{ sn_ct("sed_login Connection title",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}</h2>
              <div class="form-group">
                  <label for="inputEmail3" class="control-label">
                  	<i class="fa fa-user"></i> {{ sn_ct("sed_login User",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}
                  </label>
                  <input type="text" class="form-control" id="username" name="username">
              </div>
              
              <div class="form-group\{{ if length(error) > 0 then }} has-error\{{ endif }}">
                  <label for="password" class="control-label">
                  	<i class="fa fa-key"></i> {{ sn_ct("sed_login Password",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}
                </label>
                  <input type="password" class="form-control" id="password" name="password">
              </div>
              
              <div class="form-group">
                  <a class="password pull-left" href="?action=forget">{{ sn_ct("sed_login Forget your password ?",publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}</a>
                  <button id="btn_go" type="submit" class="btn btn-primary pull-right">
                      <i class="fa fa-check-circle left red"></i> {{ sn_ct("sed_login Login",edit:false,publish:"/publish/sn_widgets.sn",uids:widget.rows.uid,module:"sed_login")}}
                  </button>
              </div>
            <div class="clearfix"></div>
          </form>
          
      \{{ endif }}
  \{{ endif }}
</div>